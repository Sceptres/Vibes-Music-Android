plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.room'
    id("org.jetbrains.kotlin.plugin.compose") version "2.0.20"
    id 'com.google.gms.google-services'
}

// Read the admob ids from local.properties
def localProperties = new Properties()
def localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) {
    localProperties.load(new FileInputStream(localPropsFile))
} else {
    throw new GradleException("local.properties file is missing!")
}

android {
    namespace 'com.aaa.vibesmusic'
    compileSdk 34

    room {
        schemaDirectory "$projectDir/schemas"
    }

    defaultConfig {
        applicationId "com.aaa.vibesmusic"
        minSdk 24
        targetSdk 34
        versionCode 7
        versionName "1.2.3"

        // Placeholder for the app admob id
        manifestPlaceholders = [ADMOB_APP_ID: localProperties.getProperty("ADMOB_APP_ID")]

        // Build config fields for admob ad ids
        buildConfigField "String", "ADMOB_OPEN_INTERESTRIAL_AD_ID", "\"${localProperties.getProperty("ADMOB_OPEN_INTERESTRIAL_AD_ID")}\""
        buildConfigField "String", "ADMOB_IMPORT_SONGS_INTERESTRIAL_AD_ID", "\"${localProperties.getProperty("ADMOB_IMPORT_SONGS_INTERESTRIAL_AD_ID")}\""
        buildConfigField "String", "ADMOB_PLAYED_SONGS_INTERESTRIAL_AD_ID", "\"${localProperties.getProperty("ADMOB_PLAYED_SONGS_INTERESTRIAL_AD_ID")}\""
        buildConfigField "String", "ADMOB_LIBRARY_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_LIBRARY_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_MUSIC_LIBRARY_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_MUSIC_LIBRARY_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_FAVOURITE_SONGS_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_FAVOURITE_SONGS_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_PLAYLISTS_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_PLAYLISTS_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_PLAYLIST_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_PLAYLIST_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_ARTISTS_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_ARTISTS_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_ARTIST_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_ARTIST_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_ALBUMS_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_ALBUMS_SCREEN_BANNER_AD_ID")}\""
        buildConfigField "String", "ADMOB_ALBUM_SCREEN_BANNER_AD_ID", "\"${localProperties.getProperty("ADMOB_ALBUM_SCREEN_BANNER_AD_ID")}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    // Jetpack Compose dependencies
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.runtime:runtime'
    implementation 'androidx.activity:activity-compose:1.9.2'
    implementation platform('androidx.compose:compose-bom:2024.09.02')
    implementation "androidx.compose.runtime:runtime-livedata:1.7.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose"
    implementation "io.coil-kt:coil-compose:2.7.0"
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3:1.3.0'
    implementation "androidx.navigation:navigation-compose:2.8.1"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation 'androidx.compose.ui:ui-tooling'

    // Core dependencies
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.core:core-splashscreen:1.0.1"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // Room dependencies
    def room_version = "2.6.1"
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")
    implementation("androidx.room:room-rxjava2:$room_version")
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Admob dependencies
    implementation 'com.google.android.gms:play-services-ads:23.3.0'

    // Media player dependencies
    implementation "androidx.media:media:1.7.0"
    implementation "androidx.media3:media3-exoplayer:1.4.1"

    // Firebase analytics dependencies
    implementation platform('com.google.firebase:firebase-bom:33.3.0')
    implementation 'com.google.firebase:firebase-analytics'

    // Google in-app review dependencies
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'
}